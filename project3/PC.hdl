// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Register (in=feed, load=ored, out=fback, out=out);
    
    Or(a=reset, b=load, out=or1);
    Or(a=or1, b=inc, out=ored);
    
    Inc16(in=fback, out=fbackinc);
    Mux16(a=fback, b=fbackinc, sel=inc, out=fp1);

    Mux16(a=fp1, b=in, sel=load, out=fp2);

    Mux16(a=fp2, b=false, sel=reset, out=feed);
}   
